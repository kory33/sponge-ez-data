import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    ext.kotlin_version = '1.2.41'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.8.1'
}

version '0.1.0-alpha'

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    embed
    compile.extendsFrom(embed)
}

shadowJar {
    configurations = [project.configurations.embed]
    relocate 'kotlin', 'com.github.kory33.util.sponge.ezdata.kotlin.kotlin'
    archiveName = "${baseName}-${version}.${extension}"
}

task buildExample(type: ShadowJar) {
    baseName = "example"
    from(sourceSets.main.output, sourceSets.test.output)
    configurations = [project.configurations.embed]
    relocate 'kotlin', 'com.github.kory33.util.sponge.ezdata.kotlin.kotlin'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

repositories {
    mavenCentral()
}

dependencies {
    embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.spongepowered:spongeapi:7.0.0'
}

jar.enabled = false
assemble.dependsOn shadowJar

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}